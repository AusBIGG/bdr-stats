# Generates RDFDataset, Sample and Observation counts per graph
# and inserts them into the System Graph as SOSA Observations on the Real Graphs

PREFIX ex: <http://example.com/>
PREFIX olis: <https://olis.dev/>
PREFIX schema: <https://schema.org/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX tern: <https://w3id.org/tern/ontologies/tern/>
PREFIX unit: <http://qudt.org/vocab/unit/>

INSERT {
    GRAPH olis:SystemGraph {
    	?g
        	a olis:RealGraph ;
	         sosa:isFeatureOfInterestOf [
        		a sosa:Observation ;
          		sosa:observedProperty ex:DatasetsCount ;
            		sosa:resultTime ?now ;
          		sosa:hasResult [
        			a sosa:Result ;
          			schema:value ?datasets ;
             		schema:unitCode unit:UNITLESS ;
    			] ;
    		] ,
			[
        		a sosa:Observation ;
          		sosa:observedProperty ex:SamplesCount ;
            		sosa:resultTime ?now ;
          		sosa:hasResult [
        			a sosa:Result ;
          			schema:value ?samples ;
             		schema:unitCode unit:UNITLESS ;
    			] ;
    		] ,
			[
        		a sosa:Observation ;
          		sosa:observedProperty ex:ObservationsCount ;
            		sosa:resultTime ?now ;
          		sosa:hasResult [
        			a sosa:Result ;
          			schema:value ?observations ;
             		schema:unitCode unit:UNITLESS ;
    			] ;
    		] ;
        .
	}
}
WHERE {
    BIND (NOW() AS ?now)
    {
        SELECT ?g (COUNT(?o) AS ?observations)
        WHERE { 
            GRAPH ?g {
	            ?o a tern:Observation
            }
        } GROUP BY ?g
    }
    
    {
        SELECT ?g (COUNT(?d) AS ?datasets)
        WHERE { 
            GRAPH ?g {
                ?d a tern:RDFDataset
            }
        } GROUP BY ?g
    }
    
    {
        SELECT ?g (COUNT(?s) AS ?samples)
        WHERE { 
            GRAPH ?g {
	            ?s a tern:Sample
            }
        } GROUP BY ?g
    }    
}
