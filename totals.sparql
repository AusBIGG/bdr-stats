PREFIX ex: <http://example.com/>
PREFIX olis: <https://olis.dev/>
PREFIX schema: <https://schema.org/>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX tern: <https://w3id.org/tern/ontologies/tern/>
PREFIX unit: <http://qudt.org/vocab/unit/>

SELECT ?category (SUM(?val) AS ?total) (NOW() as ?query_run_time)
WHERE {
    GRAPH olis:SystemGraph {
        ?rg a olis:RealGraph ;
            sosa:isFeatureOfInterestOf [
                sosa:observedProperty ?category ;
                sosa:hasResult [
                    schema:value ?val
                ]
            ]
    }
    BIND(
        IF(?category = <https://example.com/DatasetsCount>, 1,
          IF(?category = <https://example.com/SamplesCount>, 2,
          IF(?category = <https://example.com/ObservationsCount>, 3,
          4))) AS ?sortOrder
    )
}
GROUP BY ?category ?sortOrder
ORDER BY ?sortOrder